SOT_REPO = "https://github.com/stepankuzmin/copybara-sot.git"
SOT_BRANCH = "main"

DESTINATION_REPO = "https://github.com/stepankuzmin/copybara-dst.git"
DESTINATION_BRANCH = "main"

COMMITTER = "github-actions[bot] <github-actions[bot]@users.noreply.github.com>"

PROJECT_DIR = "project"

# what can be pushed to the destination repo
PUSH_INCLUDE = [PROJECT_DIR + "/**"]
PUSH_EXCLUDE = [PROJECT_DIR + "/copy.bara.sky"]

# what can be read in the destination PR
PR_ORIGIN_INCLUDE = ["**"]
PR_ORIGIN_EXCLUDE = [".github/**"]

# what can be modified in SoT repo via PR
PR_DESTINATION_INCLUDE = [PROJECT_DIR + "/**"]
PR_DESTINATION_EXCLUDE = [PROJECT_DIR + "/copy.bara.sky", PROJECT_DIR + "/.github/**"]

PR_MESSAGE_TEMPLATE = """
${GITHUB_PR_TITLE}

${GITHUB_PR_BODY}

--

Closes: ${GITHUB_PR_URL}
"""

# Push workflow: monorepo to public repo
core.workflow(
    name = "push",
    origin = git.origin(
        url = SOT_REPO,
        ref = SOT_BRANCH
    ),
    destination = git.github_destination(
        url = DESTINATION_REPO,
        push = DESTINATION_BRANCH
    ),
    mode = "ITERATIVE",
    authoring = authoring.pass_thru(COMMITTER),
    origin_files = glob(PUSH_INCLUDE, exclude = PUSH_EXCLUDE),
    transformations = [
        core.move(PROJECT_DIR, ""),
        metadata.scrubber("\\(#", replacement = "(internal-"), # replace "#PR" with "internal-PR"
        metadata.restore_author("Original-author", separator=": ", search_all_changes = True),
    ]
)

# Pull Request workflow: public repo to monorepo
core.workflow(
    name = "pr",
    origin = git.github_pr_origin(
        url = DESTINATION_REPO,
        branch = DESTINATION_BRANCH,
        use_merge = True,
    ),
    destination = git.github_pr_destination(
        url = SOT_REPO,
        destination_ref = SOT_BRANCH,
        integrates = [],
        title = "${GITHUB_PR_TITLE}",
        update_description = True,
    ),
    mode = "CHANGE_REQUEST",
    set_rev_id = False,
    authoring = authoring.pass_thru(COMMITTER),
    origin_files = glob(PR_ORIGIN_INCLUDE, exclude = PR_ORIGIN_EXCLUDE),
    destination_files = glob(PR_DESTINATION_INCLUDE, exclude = PR_DESTINATION_EXCLUDE),
    transformations = [
        core.move("", PROJECT_DIR, paths = glob(["**"], exclude = [".github/**"])),
        metadata.squash_notes("List of changes:\n", compact = True, oldest_first = True, use_merge = True),
        # metadata.replace_message(PR_MESSAGE_TEMPLATE),
        metadata.save_author("Original-author", separator=": "),
    ]
)
