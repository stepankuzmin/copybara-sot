SOT_REPO = "https://github.com/stepankuzmin/copybara-sot.git"
SOT_BRANCH = "main"

DESTINATION_REPO = "https://github.com/stepankuzmin/copybara-dst.git"
DESTINATION_BRANCH = "main"

COMMITTER = "github-actions[bot] <github-actions[bot]@users.noreply.github.com>"

PUSH_INCLUDE = ["project/**", ".github/workflows/copybara.yml"]
PUSH_EXCLUDE = []

PR_INCLUDE = ["**"]
PR_EXCLUDE = ["copy.bara.sky", ".github/**"]

# Push workflow: monorepo to public repo
core.workflow(
    name = "push",
    description = "Push each change individually from the monorepo to the public repo",
    origin = git.origin(
        url = SOT_REPO,
        ref = SOT_BRANCH
    ),
    destination = git.github_destination(
        url = DESTINATION_REPO,
        push = DESTINATION_BRANCH
    ),
    mode = "ITERATIVE",
    authoring = authoring.pass_thru(COMMITTER),
    origin_files = glob(PUSH_INCLUDE, exclude = PUSH_EXCLUDE),
    transformations = [
        core.move("project", ""),
        metadata.expose_label("COPYBARA_INTEGRATE_REVIEW"),
        metadata.restore_author("ORIGINAL_AUTHOR", search_all_changes = True),

        # replace "#PR" with "internal-PR"
        metadata.scrubber('\\(#', replacement = '(internal-'),
    ]
)

# Pull Request workflow: public repo to monorepo
core.workflow(
    name = "pr",
    origin = git.github_pr_origin(
        url = DESTINATION_REPO,
        branch = DESTINATION_BRANCH,
    ),
    destination = git.github_pr_destination(
        url = SOT_REPO,
        destination_ref = SOT_BRANCH,
        integrates = [],
    ),
    mode = "CHANGE_REQUEST",
    set_rev_id = False,
    authoring = authoring.pass_thru(COMMITTER),
    origin_files = glob(PR_INCLUDE, exclude = PR_EXCLUDE),
    destination_files = glob(["project/**"]),
    transformations = [
        core.move("", "project"),

        metadata.save_author("ORIGINAL_AUTHOR"),
        metadata.expose_label("GITHUB_PR_NUMBER", new_name = "Closes", separator = " " + DESTINATION_REPO.replace("https://github.com/", " ").replace(".git", "#")),
    ]
)
